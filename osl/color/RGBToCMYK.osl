/*
 *  Copyright 2024 Mitch J Prater.
 *
 *  Licensed under the Apache License Version 2.0 http://apache.org/licenses/LICENSE-2.0,
 *  or the MIT license http://opensource.org/licenses/MIT, at your option.
 *
 *  This program may not be copied, modified, or distributed except according to those terms.
 */
shader color_RGBToCMYK
[[
    int rfm_nodeid = 1306958,
    string rfm_classification = "rendernode/RenderMan/pattern/OSLBook"
]]
(
    string Notes = ""
    [[
        int connectable = 0,
        string help =
            "Convert the RGB color to the CMYK color model. "
            "A color model is a way of representing a color but "
            "says nothing about what the color actually looks like. "
            "A color's appearance is instead determined by its color space. "
    ]],

    color RGB = color(0.0)
    [[
        string help =
            "The RGB color to convert to the CMYK color model. "
    ]],

    output color CMY = color(0.0),
    output float K = 0.0
)
{
    float  value = max( RGB.r, max( RGB.g, RGB.b ) );

    CMY = ( color(value) - RGB ) / value;
    K = 1.0 - value;
}
